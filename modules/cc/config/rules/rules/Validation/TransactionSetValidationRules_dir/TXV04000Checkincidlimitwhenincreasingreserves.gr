package rules.Validation.TransactionSetValidationRules_dir

@gw.rules.RuleName("TXV04000 - Check incid limit when increasing reserves")
internal class TXV04000Checkincidlimitwhenincreasingreserves {
  static function doCondition(transactionSet : entity.TransactionSet) : boolean {
    return /*start00rule*/transactionSet.Subtype =="ReserveSet"
&& transactionSet.New
//The transactionset.new condition disallows the rule from executing for all transactions associated
//with the claim (because we want the rule to execute only for the new transactions that we are trying to add)./*end00rule*/
  }

  static function doAction(transactionSet : entity.TransactionSet, actions : gw.rules.Action) {
/*start00rule*/uses org.apache.commons.lang.ObjectUtils
uses java.math.BigDecimal
uses gw.api.financials.CurrencyAmount
uses gw.api.util.CurrencyUtil
uses gw.api.financials.FinancialsCalculationUtil

var claim = transactionSet.Claim
var totalPaymentsIncludingPendingExp = FinancialsCalculationUtil.getTotalPaymentsIncludingPendingExpression()
var availableReservesExp = FinancialsCalculationUtil.getAvailableReservesExpression()
var pendingApprovalReservesExp = FinancialsCalculationUtil.getPendingApprovalReservesExpression()
var committedAndFutureAndPendingApprovalPaymentsAndReservesExpression = totalPaymentsIncludingPendingExp.plus( availableReservesExp ).plus( pendingApprovalReservesExp )
var coverageSet = new java.util.HashSet<Coverage>() // a set of Coverage to aggregate amt for that coverage
//Set the following warning parameter (warning) to true if you want to throw a warning, but still permit the end-user to continue with the transaction if he/she desires
//Set to false if you want to block this transaction
var warning = true
var currency = claim.Currency
var costType = CostType.TC_CLAIMCOST

for (trans in transactionSet.Transactions) {
  if (!trans.New) {
    continue; // in case a check is changed, and the ReserveSet gets re-validated, we don?t want to double count amounts that are already in the cache
  }
  coverageSet.add(trans.Exposure.Coverage)
}

for (coverage in coverageSet) {
  var incidentLimit = coverage.IncidentLimit
    
  var combined = FinancialsCalculationUtil.getFinancialsCalculation(committedAndFutureAndPendingApprovalPaymentsAndReservesExpression).getAmount( claim, coverage.Type, costType )
  combined = ObjectUtils.defaultIfNull( combined, CurrencyAmount.getStrict(BigDecimal.ZERO, currency )) as CurrencyAmount 

  if (combined > incidentLimit) {
    //If Warning is set to TRUE, throw a warning, but permit the transaction to go through if the user desires so     
    if (warning == true) {
      transactionSet.reject(null, null, "payment", displaykey.Rules.Validation.Transaction.ReserveExceedsIncidentLimitWarning(coverage, CurrencyUtil.renderAsCurrency(combined), coverage.Type, CurrencyUtil.renderAsCurrency(coverage.IncidentLimit) , gw.api.util.StringUtil.substring( actions.getRule().DisplayName,0, 8)))
    } else {
      //If Warning is set to FALSE, block the transaction from going through 
      transactionSet.reject("payment", displaykey.Rules.Validation.Transaction.ReserveExceedsIncidentLimitError(coverage, CurrencyUtil.renderAsCurrency(combined), coverage.Type, CurrencyUtil.renderAsCurrency(coverage.IncidentLimit), gw.api.util.StringUtil.substring( actions.getRule().DisplayName,0, 8)), null, null)
    }
  }
}/*end00rule*/
  }
}
